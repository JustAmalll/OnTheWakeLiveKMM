plugins {
    kotlin("multiplatform")
    kotlin("native.cocoapods")
    id("com.android.library")
    id("org.jetbrains.kotlin.plugin.serialization")
    id("com.squareup.sqldelight")
}

kotlin {
    android()
    iosX64()
    iosArm64()
    iosSimulatorArm64()

    cocoapods {
        summary = "Some description for the Shared Module"
        homepage = "Link to the Shared Module homepage"
        version = "1.0"
        ios.deploymentTarget = "14.1"
        podfile = project.file("../iosApp/Podfile")
        framework {
            isStatic = false
            baseName = "shared"
        }
    }

    sourceSets {
        val commonMain by getting {
            dependencies {
                implementation(SharedDeps.ktorCore)
                implementation(SharedDeps.ktorWebSockets)
                implementation(SharedDeps.ktorLogging)
                implementation(SharedDeps.ktorSerialization)
                implementation(SharedDeps.ktorSerializationJson)

                implementation(SharedDeps.kmmSettings)
                implementation(SharedDeps.kmmSettingsCoroutines)

                implementation(SharedDeps.kotlinDateTime)

                implementation(SharedDeps.serialization)

                implementation(SharedDeps.sqlDelightRuntime)
                implementation(SharedDeps.sqlDelightCoroutinesExtensions)
            }
        }
        val commonTest by getting {
            dependencies {
                implementation(kotlin("test"))
            }
        }
        val androidMain by getting {
            dependencies {
                implementation(AndroidDeps.ktorCIO)
                implementation(AndroidDeps.sqlDelightAndroidDriver)
            }
        }
        val androidTest by getting
        val iosX64Main by getting
        val iosArm64Main by getting
        val iosSimulatorArm64Main by getting
        val iosMain by creating {
            dependsOn(commonMain)
            iosX64Main.dependsOn(this)
            iosArm64Main.dependsOn(this)
            iosSimulatorArm64Main.dependsOn(this)

            dependencies {
                implementation(IosDeps.ktorIOS)
                implementation(IosDeps.sqlDelightNativeDriver)
            }
        }
        val iosX64Test by getting
        val iosArm64Test by getting
        val iosSimulatorArm64Test by getting
        val iosTest by creating {
            dependsOn(commonTest)
            iosX64Test.dependsOn(this)
            iosArm64Test.dependsOn(this)
            iosSimulatorArm64Test.dependsOn(this)
        }
    }
}

android {
    namespace = "dev.amal.onthewakelivekmm"
    compileSdk = 33
    defaultConfig {
        minSdk = 24
        targetSdk = 33
    }
}

sqldelight {
    database("OnTheWakeLiveDatabase") {
        packageName = "dev.amal.onthewakelivekmm.database"
        sourceFolders = listOf("sqldelight")
    }
}